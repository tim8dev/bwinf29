startshape BAUM
background {b 1}

#Rekursionsstart, zeichnet einen Pythagorasbaum
rule BAUM {
 AST {b 0.5 h 0 sat .8}
}
#Hauptregel, zeichnen Viereck und Hilfsregeln
rule AST {
 SQUARE {}
#Unterregel: Winkelast, also ein Ast mit speziellem Winkel, z.B. 45°
 WINKELAST{}
}

#Bemerkung: alpha ist der linke Winkel über dem Quadrat.

#Teilregel, zeichnet einen Ast mit alpha=45°
rule WINKELAST {
 #nach (rechts dunkler; links heller)
 AST{x  .5 y 1 size (1/sqrt(2)) r -45 b -.2}
 AST{x -.5 y 1 size (1/sqrt(2)) r  45 b +.25}
}

#Teilregel, zeichnet einen Ast mit alpha=30°
rule WINKELAST 1.8 {
 #nach (rechts röter; links lilaner)
 AST{x( .5+sin(15)/sqrt(8))         y(sin(75)/sqrt(8)        +.5) size      (1/2) r -60 h -6}
 AST{x(-.5+sin(15)/sqrt(8)*sqrt(3)) y(sin(75)/sqrt(8)*sqrt(3)+.5) size(sqrt(3)/2) r  30 h +38}
}

#Teilregel, zeichnet einen Ast mit alpha=60°
rule WINKELAST 1 {
 #nach (rechts ungesättigter; links gesättigter)
 #aber nie unter sat 0.2, oder über sat 1.
 AST{x(-.5-sin(15)/sqrt(8))         y(sin(75)/sqrt(8)        +.5) size     (1 /2) r  60 sat -.03141|sat 0.1}
 AST{x( .5-sin(15)/sqrt(8)*sqrt(3)) y(sin(75)/sqrt(8)*sqrt(3)+.5) size(sqrt(3)/2) r -30 sat +.03141|sat 1}
}
